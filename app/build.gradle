apply plugin: 'com.android.application'

android {
	compileSdkVersion 19
	buildToolsVersion '19.1.0'

	defaultConfig {
		minSdkVersion 14
		targetSdkVersion 19

//      TODO [on release] Update version
		versionCode 7
		versionName '2.3'

		testPackageName 'com.iliakplv.notes.tests'
		testInstrumentationRunner 'android.test.InstrumentationTestRunner'
	}

	signingConfigs {
		release {
//          TODO [on release] Replace with actual values
			keyAlias 'KEY_ALIAS'
			keyPassword 'KEY_PASSWORD'
			storeFile file('KEYSTORE_FILE')
			storePassword 'KEYSTORE_PASSWORD'
		}
	}

	buildTypes {
		debug {
			runProguard false
			zipAlign true
			debuggable true
		}
		release {
			runProguard true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
			zipAlign true
			debuggable false
			signingConfig signingConfigs.release
		}
	}

	testOptions {
		resultsDir = "$project.buildDir/tests"
	}
}

task nativeLibsToJar(type: Zip) {
	destinationDir file("$buildDir/native-libs")
	baseName 'native-libs'
	extension 'jar'
	from fileTree(dir: 'libs', include: '**/*.so')
	into 'lib/'
}

tasks.withType(Compile) {
	compileTask -> compileTask.dependsOn(nativeLibsToJar)
}

dependencies {
	compile 'com.android.support:support-v4:20.+'
	compile 'joda-time:joda-time:2.3'

	compile files('libs/dropbox-sync-sdk-android.jar')
	compile files("$buildDir/native-libs/native-libs.jar")
}
